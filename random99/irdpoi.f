CDECK  ID>, IRDPOI.
      FUNCTION IRDPOI ( A )
C-----------------------------------------------------------------------
C
C...       FUNCTION IRDPOI ( A )                            Ch.Walck    001005
C.
C.    Random number from Poisson distribution with mean A 
C.    (this is an internal double precision version, cf IRNPOI).
C.
C.    P(R;A) = A**R * EXP(-A) / R!
C.
C.    Variable:          R >= 0 (integer)
C.    Parameter:         A > 0  (real)
C.
C.    Expectation value: E(R) = A
C.    Variance:          V(R) = A
C.    Skewness:         G1(R) = 1/SQRT(A)
C.    Kurtosis:         G2(R) = 1/A
C.
C.    While checking thoroughly extreme value distributions for the
C.    Poisson distribution unwanted tail-effects were noticed due to
C.    the discreteness of 24-bit random numbers in the usual IRNPOI
C.    routine in patch RNLIB. This version cures this by using double 
C.    precision arithmetics and random numbers generated by RNDBLE 
C.    internally at the expense of a slightly slower performance.
C.
C.                                            /Ch. Walck 2000-10-05
C.
C.----------------------------------------------------------------------
      SAVE AMAX, AREF, EXPA
      DOUBLE PRECISION EXPA, P, PI, R, RDNRMT, RNDBLE
C     AMAX set to 88 since EXP(-X)=0 if X>88.5 on a VAX! / CHW 860804
      DATA AREF/-1.0/, AMAX/88.0/
C--
C--   Normal approximation for large means
C--
      IF ( A .GT. AMAX ) THEN
         IRN = RDNRMT ( A, SQRT(A) ) + 0.5
C--
C--   Get uniform r.n. and loop until Poisson cumulant above
C--
      ELSE
         R  = RNDBLE(DUMMY)
         I  = 0
         P  = 0.0D0
         IF ( A .EQ. AREF ) GO TO 10
         AREF = A
         EXPA = DEXP(-DBLE(A))
   10    PI = EXPA
   20    P  = P + PI
         IF ( R.LT.P .OR. PI.EQ.0.0D0 ) THEN
            IRN = I
            GO TO 30
         ELSE
            I  = I + 1
            PI = PI * A / DFLOAT(I)
            GO TO 20
         END IF
      END IF
   30 IRDPOI = IRN
      RETURN
      END
